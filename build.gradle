buildscript {
    ext.versions = [
        kotlin: '1.8.10',
        dokka: '1.8.10',
        conscrypt: '2.5.2',
        compose: '1.4.4',
        composeBom: '2023.01.00',
        accompanist: '0.30.1'
    ]

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'

android {
    namespace "at.bitfire.cert4android"

    compileSdkVersion 33
    buildToolsVersion '33.0.2'

    defaultConfig {
        minSdkVersion 21            // Android 5
        targetSdkVersion 33         // Android 13
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures.compose = true

    composeOptions {
        kotlinCompilerExtensionVersion = "${versions.compose}"
    }

    lint {
        disable 'MissingTranslation', 'ExtraTranslation'
    }


    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"

    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Jetpack Compose - BOM
    def composeBom = platform("androidx.compose:compose-bom:${versions.composeBom}")
    implementation composeBom
    androidTestImplementation composeBom

    // Jetpack Compose - Material 3
    implementation 'androidx.compose.material3:material3'

    // Jetpack Compose - LiveData Support
    implementation 'androidx.compose.runtime:runtime-livedata'

    // Jetpack Compose - Activity Support
    implementation 'androidx.activity:activity-compose:1.7.0'

    // Jetpack Compose - ViewModel Support
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1'

    // Jetpack Compose - Accompanist libraries
    implementation "com.google.accompanist:accompanist-themeadapter-material:${versions.accompanist}"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation "org.conscrypt:conscrypt-android:${versions.conscrypt}"

    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'

    testImplementation 'junit:junit:4.13.2'
}